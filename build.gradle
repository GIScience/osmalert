ext {
    jupiterVersion = '5.9.3' // determined by spring-boot-dependencies
    mockitoVersion = '5.3.1' // determined by spring-boot-dependencies
    assertJVersion = '3.24.2'
}

subprojects { project ->

    apply plugin: 'java'

    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation("org.junit.jupiter:junit-jupiter:$jupiterVersion")
        testImplementation("org.assertj:assertj-core:$assertJVersion")
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    test {
        jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
        jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
        useJUnitPlatform()
    }

    compileTestJava {
        options.compilerArgs += '-parameters'
    }

    jacocoTestReport {
        dependsOn test

        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, include: 'org/heigit/**')
        }))
    }


    jacocoTestCoverageVerification {
        dependsOn test

        violationRules {
            rule {
                limit {
                    enabled = true
                    minimum = 0.75
                }
            }
        }
    }

}

wrapper {
    gradleVersion = '8.3'
}